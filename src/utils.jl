# ------------------------------------------------------------------
# Licensed under the ISC License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    readgeotable(args; coordnames=(:x,:y,:z), kwargs)

Read data from disk using `CSV.File`, optionally specifying
the columns `coordnames` with spatial coordinates.

The arguments `args` and keyword arguments `kwargs` are
forwarded to the `CSV.File` function, please check their
documentation for more details.
"""
readgeotable(args...; coordnames=(:x,:y,:z), kwargs...) =
  georef(DataFrame!(CSV.File(args...; kwargs...)), coordnames)

"""
    split(object, fraction, [normal])

Split spatial `object` into two parts where the first
part has a `fraction` of the total volume. The split
is performed along a `normal` direction. The default
direction is aligned with the first spatial dimension
of the object.
"""
function split(object, fraction::Real, normal=nothing)
  if isnothing(normal)
    partition(object, FractionPartitioner(fraction))
  else
    partition(object, BisectFractionPartitioner(normal, fraction))
  end
end

"""
    groupby(sdata, var)

Partition spatial data `sdata` into groups of constant value
for spatial variable `var`.

### Notes

Missing values are grouped into a separate group.
"""
groupby(sdata::AbstractData, var::Symbol) =
  partition(sdata, VariablePartitioner(var))

"""
    boundbox(object)

Return the minimum axis-aligned bounding rectangle of the spatial `object`.

### Notes

Equivalent to `cover(object, RectangleCoverer())`
"""
boundbox(object) = cover(object, RectangleCoverer())

"""
    sample(object, nsamples, [weights], replace=false)

Generate `nsamples` samples from spatial `object`
uniformly or using `weights`, with or without
replacement depending on `replace` option.
"""
function sample(object::Union{AbstractDomain,AbstractData}, nsamples::Int,
                weights::AbstractVector=[]; replace=false)
  if isempty(weights)
    sample(object, UniformSampler(nsamples, replace))
  else
    sample(object, WeightedSampler(nsamples, weights, replace))
  end
end

"""
    join(sdata‚ÇÅ, sdata‚ÇÇ)

Join variables in spatial data `sdata‚ÇÅ` and `sdata‚ÇÇ`.
"""
join(sdata‚ÇÅ::AbstractData, sdata‚ÇÇ::AbstractData) =
  join(sdata‚ÇÅ, sdata‚ÇÇ, VariableJoiner())

"""
    filter(pred, sdata)

Retain all locations in spatial data `sdata` according to
a predicate function `pred`. A predicate function takes
table rows as input, e.g. `pred(r) = r.state == "CA"`.
"""
function filter(pred, sdata::AbstractData)
  ùíØ = values(sdata)
  ùíü = domain(sdata)

  # row table view
  ctor = Tables.materializer(ùíØ)
  rows = Tables.rows(ùíØ)

  # locations to retain
  locs = findall(pred, rows)

  # return point set
  table = ctor(rows[locs])
  coord = coordinates(ùíü, locs)

  georef(table, coord)
end

"""
    slice(object, xmin:xmax, ymin:ymax, ...)

Slice spatial `object` using real coordinate ranges
`xmin:xmax`, `ymin:ymax`, ...
"""
slice(object, ranges::Vararg) =
  inside(object, Rectangle(first.(ranges), last.(ranges)))

"""
    spheredir(Œ∏, œÜ)

Returns the 3D direction given polar angle `Œ∏` and
azimuthal angle `œÜ` in degrees according to the ISO
convention.
"""
function spheredir(theta, phi)
  Œ∏, œÜ = deg2rad(theta), deg2rad(phi)
  SVector(sin(Œ∏)*cos(œÜ), sin(Œ∏)*sin(œÜ), cos(Œ∏))
end
